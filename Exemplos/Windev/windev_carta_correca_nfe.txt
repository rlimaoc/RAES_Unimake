//PROCEDURE uninfedll_carta_correcao_nfe(sRecebe_certificado_arquivo is string="",sRecebe_certificado_senha is string="", ...
          sRecebe_Thumbprint is string="",sRecebe_sErialnumber is string="",nRecebe_numero_lote is int=0, ... 
          srecebe_xcorrecao is string="",nrecebe_COrgao_cuf is int=0,sRecebe_ChNFe is string="", ...
          sRecebe_cnpj is string="",nrecebe_TpAmb is int=0,nSequencia_evento is int=0, ... 
          sRecebe_caminho_xml is string="",srecebe_caminho_retorno is string="")
          

sretorno_xml is string=""
sXml_assinado is string=""
sRetorno_erro is string=""
sRetorno_xml_transmitido_obs is string=""
sRetorno_xml_transmitido is string=""

sNome_arquivo is string=""
sLocalizacao_xml_retornado is string=""
sRetorno_correcao_tpAmb is string=""
sRetorno_correcao_cStat is string=""
sRetorno_correcao_xMotivo is string=""
sRetorno_correcao_nSeqEvento is string=""
sRetorno_correcao_nProt is string=""


O_CONFIGURACAO is Configuracao
IF sRecebe_certificado_arquivo="" THEN
	IF sRecebe_Thumbprint="" AND sRecebe_sErialnumber="" THEN
		vEr_certificado is Variant=SELECIONA_CERTIFICADO()
		O_CONFIGURACAO.CertificadoDigital=vEr_certificado
	ELSE
		o_CertificadoDigital is CertificadoDigital
		vEr_certificado is Variant=o_CertificadoDigital.BuscarCertificadoDigital(sRecebe_sErialnumber)
		O_CONFIGURACAO.CertificadoDigital=vEr_certificado
		busca_token is string =INIRead("certificado","token","",fExeDir()+"\certtoken.ini")
		IF busca_token<>"ok" THEN
			o_CertA3 is Unimake.Business.DFe.Security.ClsX509Certificate2ExtensionInterop()	
			o_CertA3.SetPinPrivateKey(O_CONFIGURACAO,sRecebe_certificado_senha)		
			INIWrite("certificado","token","ok",fExeDir()+"\certtoken.ini") 
		END
	END
ELSE	
	O_CONFIGURACAO.CertificadoArquivo=sRecebe_certificado_arquivo//"D:\erpmatos\certificados\FABIANE.pfx"
	O_CONFIGURACAO.CertificadoSenha=sRecebe_certificado_senha//
END

O_CONFIGURACAO.TipoDFe=0 // 0=nfe 1-nfce
O_CONFIGURACAO.Servico=5 // 5-carta correcao = evento 3-Inutilizacao Nfe 5=envio evento 6 // autorizacao nfe

O_EnvEvento is Unimake.Business.DFe.Xml.nFe.EnvEvento() //O_EnvEvento is EnvEvento
O_EnvEvento.Versao = "1.00"
O_EnvEvento.IdLote = NumToString(nRecebe_numero_lote,"015d") // "000000000000001"

//* =================================================
//* EVENTO NÃšMERO 1
//* =================================================
//* Criar tag Evento

O_Evento is Unimake.Business.DFe.Xml.nFe.Evento()
O_Evento.Versao = "1.00"

O_DetEventoCCE is Unimake.Business.DFe.Xml.NFe.DetEventoCCE()
O_DetEventoCCE.Versao = "1.00"
O_DetEventoCCE.XCorrecao=srecebe_xcorrecao

o_InfEvento  is Unimake.Business.DFe.Xml.NFe.InfEvento()
//o_InfEvento=O_DetEventoCanc
o_InfEvento.DetEvento=O_DetEventoCCE

data_transmissao is DateTime=""
dDate is Date = DateSys()
hora is Time=Now()
data_transmissao..Day=dDate..Day
data_transmissao..Month=dDate..Month
data_transmissao..Year=dDate..Year
data_transmissao..Hour=hora..Hour
data_transmissao..Minute=hora..Minute
data_transmissao..Second=hora..Second

o_InfEvento:COrgao = nrecebe_COrgao_cuf 
o_InfEvento:ChNFe = sRecebe_ChNFe 
o_InfEvento:CNPJ = sRecebe_cnpj
o_InfEvento:DhEvento = data_transmissao
o_InfEvento:TpEvento = 110110 // TipoEventoNFe.CartaCorrecao
o_InfEvento:NSeqEvento = nSequencia_evento
o_InfEvento:VerEvento = "1.00"
o_InfEvento:TpAmb = nrecebe_TpAmb

O_Evento.InfEvento=o_InfEvento
O_EnvEvento.AddEvento(O_Evento)

WHEN EXCEPTION IN 
	O_RecepcaoEvento is Unimake.Business.DFe.Servicos.NFe.RecepcaoEvento(O_EnvEvento,O_CONFIGURACAO) // 	
	O_RecepcaoEvento.Executar(O_EnvEvento,O_CONFIGURACAO)
	
	sretorno_xml=O_RecepcaoEvento.RetornoWSString
	sXml_assinado=O_RecepcaoEvento.GetConteudoXMLAssinado()
	
	O_RecepcaoEvento.GravarXmlDistribuicao(srecebe_caminho_retorno);
	
	sNome_arquivo=sRecebe_ChNFe+"_110110_"+NumToString(nSequencia_evento,"02d")+"-proceventonfe.xml"
	sLocalizacao_xml_retornado=srecebe_caminho_retorno+sNome_arquivo
	sRetorno_xml_transmitido_obs=fLoadText(sLocalizacao_xml_retornado)
	
	XMLDocument("xml_correcao",sRetorno_xml_transmitido_obs)
	sRetorno_correcao_tpAmb= XMLRead("xml_correcao","/procEventoNFe/retEvento/infEvento/tpAmb")
	sRetorno_correcao_cStat= XMLRead("xml_correcao","/procEventoNFe/retEvento/infEvento/cStat")
	sRetorno_correcao_xMotivo= XMLRead("xml_correcao","/procEventoNFe/retEvento/infEvento/xMotivo")
	sRetorno_correcao_nSeqEvento= XMLRead("xml_correcao","/procEventoNFe/retEvento/infEvento/nSeqEvento")
	sRetorno_correcao_nProt= XMLRead("xml_correcao","/procEventoNFe/retEvento/infEvento/nProt")
	XMLClose("xml_correcao")
	
	IF sRetorno_correcao_nProt<>"" THEN
		O_RecepcaoEvento.GravarXmlDistribuicao(sRecebe_caminho_xml)
		sLocalizacao_xml_retornado=sRecebe_caminho_xml+sNome_arquivo
		sRetorno_xml_transmitido=fLoadText(sLocalizacao_xml_retornado)
	END
DO
	sRetorno_erro="ERRO:"+ExceptionInfo(errFullDetails)
END

RESULT(sretorno_xml,sXml_assinado,sRetorno_xml_transmitido_obs,sNome_arquivo,sRetorno_xml_transmitido,... 
      sRetorno_correcao_cStat,sRetorno_correcao_xMotivo,sRetorno_erro)

